buildscript {
    repositories {
        maven {
            url 'https://jetbrains.jfrog.io/jetbrains/plugins-release'
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }

    }
    dependencies {
        //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
    }
}

apply plugin: 'base'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: "com.jfrog.artifactory"

apply from: "$rootDir/gradle/common/dependencies.gradle"

description = 'Spek Distribution packages'

evaluationDependsOn ':spek-api'
evaluationDependsOn ':spek-junit-engine'

publishing {
    publications {
        api(MavenPublication) {
            groupId = 'org.jetbrains.spek'
            artifactId = 'spek-api'
            version = rootProject.version

            from project(':spek-api').components.java
            artifact project(':spek-api').tasks['sourceJar']

            pom.withXml {
                asNode().dependencies.dependency.each {
                    it.scope[0].value = ('compile')
                }
            }
        }

        engine(MavenPublication) {
            groupId = 'org.jetbrains.spek'
            artifactId = 'spek-junit-engine'
            version = rootProject.version

            from project(':spek-junit-engine').components.java
            artifact project(':spek-junit-engine').tasks['sourceJar']

            pom.withXml {
                asNode().dependencies.dependency.each {
                    it.scope[0].value = ('compile')
                }
            }
        }
    }
}

bintray {
    user = "${rootProject.ext.bintrayUser}"
    key = "${rootProject.ext.bintrayApiKey}"
    publish = false

    pkg {
        repo = 'maven'
        desc = "Specification framework for the JVM"
        name = 'spek'
        licenses = ['BSD']
        labels = ['kotlin', 'testing', 'specification-framework']
        vcsUrl = 'https://github.com/JetBrains/spek.git'

        githubRepo = 'JetBrains/spek'


        version {
            name = project.version
        }
    }

    publications = ['api', 'engine']
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            repoKey = 'public-snapshots'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            publications('api', 'engine')
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
    }
}